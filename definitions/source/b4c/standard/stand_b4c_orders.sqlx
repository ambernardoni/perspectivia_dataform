--------------------------------------------------------------------------------------------------
  -- Converte as informações no formato da plataforma para o formato da PERSPECTIVIA
  -- A maior parte da lógica de transformação de formatos está aqui.
  -- DONWSTREAM: Essa view será usada pelo processamento geral (independente de plataforma]
--------------------------------------------------------------------------------------------------

config {
    type: "view",
    schema: "standard",
    description: "Cleaned up version of orders",
    tags: ['b4c']
}

SELECT
  _airbyte_extracted_at,
  SAFE_CAST(codigo AS STRING) AS order_id,
  SAFE_CAST(data_criacao AS DATETIME) AS creation_date,
  SAFE_CAST(data_criacao AS DATETIME) AS update_date,   

--  ${order_status_type()} AS status_type,
  ${order_status_group_type()} AS status_type_category,
  descricao_status AS status_type_detail,
  canal AS point_of_sale, 
  

  -------------  value ---------------
  STRUCT ( 
    SAFE_CAST(total_pedido AS FLOAT64) AS total,
    SAFE_CAST(subtotal AS FLOAT64) AS items,
    SAFE_CAST(descontos AS FLOAT64) AS discounts,
    SAFE_CAST(valor_frete AS FLOAT64) AS shipping,
    0 AS tax 
  ) AS value,

  -------------  payment -------------
  STRUCT ( 
 --   ${payment_status_type()}  AS status_type,
    JSON_EXTRACT_SCALAR(pagamentos, '$[0].status') AS status_detail,
    JSON_VALUE(pagamentos[0], '$.nome_meio_pagamento') AS method,
    JSON_VALUE(pagamentos[0], '$.nome_forma_pagamento') AS method_detail 
  ) AS payment,

  -------------  address -------------
  STRUCT ( 
    JSON_VALUE(dados_da_entrega, '$.cep') AS shipping_zipcode,
    JSON_VALUE(dados_da_entrega, '$.cidade') AS shipping_city,
    JSON_VALUE(dados_da_entrega, '$.uf') AS shipping_state,
    "Brasil" AS shipping_country 
  ) AS address,
  
  -------------  customer ------------
  STRUCT (
    JSON_VALUE(dados_do_cliente, '$.codigo_cliente') AS customer_id,
    JSON_VALUE(dados_do_cliente, '$.nome') AS name,
    JSON_VALUE(dados_do_cliente, '$.cpf_cnpj') AS doc_number,
    JSON_VALUE(dados_do_cliente, '$.email') AS email,
    JSON_VALUE(dados_do_cliente, '$.fone') AS phone 
  ) AS customer

FROM
  ${ref("stg_b4c_orders_all")} O
--  ${ref("raw_b4c_Orders_hist")} O




-------------------------------------------------------------------------------
--------------------------  DEFINIÇÂO DE RECEITA APROVADA ---------------------

js {
// Essa informação é usada para definir o status geral do pedido na tabelas orders (orders.sqlx)
// rowConditions: [
//        'status_type IN ("Aprovado", "Cancelado", "Pendente", "Outros")',
//        'payment.status_type IN ("Aprovado", "Cancelado", "Pendente", "Outros")',
//        'status_type_category IN ("Aprovado", "Cancelado", "Pendente")'
// ]
// SQL
//    WHEN payment.status_type = "Cancelado" OR status_type = "Cancelado" THEN "Cancelado"
//    WHEN payment.status_type = "Aprovado" AND status_type <> "Cancelado" THEN "Aprovado"
//    ELSE "Pendente" 

//-----------------------------------------------


// Isso não foi totalmente refinado - entender melhor no futuro
function order_status_group_type(){
  return `
  case
    when descricao_status in ('Liberado Financeiro', 'Na Expedição', 'Nota Fiscal Emitida', 'Pedido Entregue', 'Disponível para retirada', 'Em Transporte') then 'Aprovado'

    when descricao_status in ('O pagamento não foi finalizado', 'Pedido cancelado', 'Problema no Boleto', 
      'Problemas com o Cartão', 'Cancelamento Confirmado', 'Pedido Estornado Boleto', 'Pedido Estornado Cartão') then 'Cancelado'
    when descricao_status in ('Aguardando Pagamento', 'Transação em Andamento') then 'Pendente'
    else 'Outros'
  end`; 

}



// status_type [Aprovado, Cancelado, Pendente Outros]
function order_status_type(){
  return `
  case
    when descricao_status in ('Liberado Financeiro', 'Na Expedição', 'Nota Fiscal Emitida', 'Pedido Entregue', 'Disponível para retirada', 'Em Transporte') then 'Aprovado'

    when descricao_status in ('O pagamento não foi finalizado', 'Pedido cancelado', 'Problema no Boleto', 
      'Problemas com o Cartão', 'Cancelamento Confirmado', 'Pedido Estornado Boleto', 'Pedido Estornado Cartão') then 'Cancelado'
    when descricao_status in ('Aguardando Pagamento', 'Transação em Andamento') then 'Pendente'
    else 'Outros'
  end`; }
// O que fazer com "Aguradando pagamento"?

//-----------------------------------------------
// paymet.status_type [Confirmado, Cancelado, Pendente, Outros]
function payment_status_type(){
  return `
  case
    when JSON_EXTRACT_SCALAR(pagamentos, '$[0].status') in ('Autorizado', 'Capturado/Confirmado') then 'Aprovado'
    when JSON_EXTRACT_SCALAR(pagamentos, '$[0].status') in ('Erro', 'Cancelado') then 'Cancelado'
    else 'Outros'
  end`; }


}

