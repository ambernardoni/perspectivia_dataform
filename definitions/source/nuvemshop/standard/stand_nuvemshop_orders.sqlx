--------------------------------------------------------------------------------------------------
  -- Converte as informações no formato da plataforma para o formato da PERSPECTIVIA
  -- A maior parte da lógica de transformação de formatos está aqui.
  -- DONWSTREAM: Essa view será usada pelo processamento geral (independente de plataforma]
--------------------------------------------------------------------------------------------------

config {
    type: "view",
    schema: "standard",
    description: "Cleaned up version of orders",
    tags: ['nuvemshop'],
    disabled: false
}

SELECT
  _airbyte_extracted_at,
  SAFE_CAST(number AS STRING) AS order_id,

  -- IMPORTANTE AQUI - Se o formato for TIMESTAMP ou outro formado de data fora do fuso Brasil, trazer para o fuso Brasil
  DATETIME(SAFE_CAST(created_at AS TIMESTAMP), "America/Sao_Paulo") AS creation_date,
  DATETIME(SAFE_CAST(updated_at AS TIMESTAMP), "America/Sao_Paulo") AS update_date,

  -- Status
  ${calc_status_type_category()} AS status_type_category,
  status AS status_type_detail,
  
  storefront AS point_of_sale, 

  -------------  value ---------------  
  STRUCT 
  (
    SAFE_CAST(total AS FLOAT64) AS total,
    0 AS items,    --#############
    SAFE_CAST(discount AS FLOAT64) AS discounts,
    SAFE_CAST(shipping_cost_customer AS FLOAT64) AS shipping,
    0 AS tax
  ) 
  AS value,
 -------------  payment -------------  
  STRUCT 
  (
    payment_status AS status_detail,
    JSON_VALUE (payment_details ,"$.method") AS method,
    JSON_VALUE (payment_details ,"$.credit_card_company") AS method_detail
  )
  AS payment,
  -------------  address -------------  
  STRUCT 
  (
    billing_zipcode AS shipping_zipcode,
    JSON_VALUE(shipping_address, '$.city') AS shipping_city,
    JSON_VALUE(shipping_address, '$.province') AS shipping_state,
    JSON_VALUE(shipping_address, '$.country')AS shipping_country
  ) 
  AS address,

  -------------  customer ------------  
  STRUCT 
  (
    JSON_VALUE(customer, '$.id')  AS customer_id,
    contact_name AS name,
    contact_identification AS doc_number,
    contact_email AS email,
    contact_phone AS phone
  ) 
  AS customer,

FROM
  ${ref("stg_nuvemshop_orders_all")} O


-------------------------------------------------------------------------------
--------------------------  DEFINIÇÂO DE RECEITA APROVADA ---------------------

js {
// -------------------------------------------------------------------
// A tray tem dois status: do pedido e do pagamento
// PEDIDO
//   ('closed') then 'Aprovado'
//   ('cancelled') then 'Cancelado'
//   ('open') then 'Pendente'
// PAGAMENTO
//   ('paid') then 'Aprovado'
//   ('refunded') then 'Cancelado'
//   ('pending') then 'Pendente'
//
// REGRA DE NEGÓCIO (isso bate com a regra da Nuvemshop de receita aprovada)
//   (payment_status IN ('cancelled') OR (status IN ('cancelled'))) THEN 'Cancelado' 
//   WHEN (payment_status IN ('paid') AND (status NOT IN ('cancelled'))) THEN 'Aprovado'
//   ELSE "Pendente" 

//-----------------------------------------------
// status_type_category [Aprovado, Cancelado, Pendente, Outros]
function calc_status_type_category(){
  return `
      CASE 
        WHEN (payment_status IN ('cancelled') OR (status IN ('cancelled'))) THEN 'Cancelado' 
        WHEN (payment_status IN ('paid') AND (status NOT IN ('cancelled'))) THEN 'Aprovado' 
        ELSE "Pendente" 
      END`; }

}
