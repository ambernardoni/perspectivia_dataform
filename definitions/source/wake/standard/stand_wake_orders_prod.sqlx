config {
    description: "Padronização da tabela de produtos por pedido",
    schema: "standard",
    type: "view",
    columns: {
        order_id: "Identificador único do pedido",
        product_id: "Identificador único do produto",
        quantity: "Quantidade do produto no pedido",
        price_unit: "Preço unitário do produto",
        price_discount: "Valor do desconto aplicado ao produto",
        price_total: "Preço total do produto no pedido"
    },
    tags: ['wake']
}

-----------------------------------------------------------------------------------------------------------
-- Colocando aqui código para para de-duplicar pedidos.
-- as demais deduplicações de plataforma estão na camada staging, mas essa aqui não teve muito jeito.
-- Não sei o quanto isso se encaixa aqui com uma estratégia futura de carga incremental... que deveria ser no orders
--   mas não quis de-duplicar essa tabela depois, pois não sei se a forma de localizar registros duplicados (order_id+product_id+sku)
--   é confiável e não vai introduzir novos problemas...
WITH
  order_deduplicated AS (
  SELECT
    *,
    ROW_NUMBER() OVER (PARTITION BY pedidoId ORDER BY _airbyte_extracted_at DESC) AS rn
  FROM
    ${ref("raw_wake_Orders")} prod
)

-----------------------------------------------------------------------------------------------------------------

  SELECT
  
   SAFE_CAST(O.pedidoId AS STRING)  as order_id,
   SAFE_CAST(JSON_VALUE(json_item, "$.produtoVarianteId") AS STRING) AS product_id,
   SAFE_CAST(JSON_VALUE(json_item, "$.sku") AS STRING) AS product_sku,

   -- Dados do pedido
   SAFE_CAST(JSON_VALUE(json_item, "$.quantidade") AS INT64) AS quantity,
   SAFE_CAST(JSON_VALUE(json_item, "$.precoVenda") AS FLOAT64) AS price_unit,
   0 as price_discount,
   (
   SAFE_CAST(JSON_VALUE(json_item, "$.quantidade") AS FLOAT64) *
   SAFE_CAST(JSON_VALUE(json_item, "$.precoVenda") AS FLOAT64) 
   ) AS price_total

FROM
    -- tabela de pedidos 0 items abertos
    order_deduplicated O,
    UNNEST(JSON_EXTRACT_ARRAY(itens, '$')) AS json_item
WHERE
  rn =1