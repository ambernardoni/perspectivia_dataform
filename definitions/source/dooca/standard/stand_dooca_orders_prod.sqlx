config {
    description: "Padronização da tabela de produtos por pedido",
    schema: "standard",
    type: "view",
    columns: {
        order_id: "Identificador único do pedido (string)",
        product_id: "Identificador único do produto (string)",
        quantity: "Quantidade do produto no pedido (int64)",
        price_unit: "Preço unitário do produto (float64)",
        price_discount: "Valor do desconto aplicado ao produto (float64)",
        price_total: "Preço total do produto no pedido (float64)"
    },
    tags: ['dooca']
}

  SELECT
    SAFE_CAST(O.id AS STRING) AS order_id,
    SAFE_CAST(JSON_VALUE(json_item, "$.product_id") AS STRING) AS product_id,

    SAFE_CAST(JSON_VALUE(json_item, "$.quantity") AS INT64)  AS quantity,
    SAFE_CAST(JSON_VALUE(json_item, "$.price") AS FLOAT64)  AS price_unit,
    SAFE_CAST(JSON_VALUE(json_item, "$.discount") AS FLOAT64)  AS price_discount,
    SAFE_CAST(JSON_VALUE(json_item, "$.total") AS FLOAT64)  AS price_total
  FROM
    ${ref("raw_dooca_Orders")} O,
    UNNEST(JSON_EXTRACT_ARRAY(O.items, '$')) AS json_item
