config {
    type: "view",
    schema: "standard",
    description: "Cleaned up version of orders",
    tags: ['dooca'],
    disabled: false
}

-- Este script organiza todas as informações de pedidos. 
-- É ele que converte as informações no formato da plataforma para o formato da PERSPECTIVIA
-- A maior parte da lógica de transformação está aqui.


SELECT

  SAFE_CAST(id AS STRING) AS order_id,
  SAFE_CAST(created_at AS TIMESTAMP) AS creation_date,
  SAFE_CAST(updated_at AS TIMESTAMP) AS update_date,

  type AS order_type,          -- Dooca guarda devolução de alguma forma?
  type as order_type_detail,
  ${dooca.status_type_group("status")} AS status_type,
  status AS status_type_detail,     

  "e-commerce" AS point_of_sale, -- Dooca não guarda outros parâmteros aqui?

  ( 
    SELECT sum (SAFE_CAST(JSON_VALUE(json_items,"$.quantity") AS INT64)) 
      FROM UNNEST(JSON_EXTRACT_ARRAY(items, '$')) AS json_items
  )  AS quantity_items,
  (
    SELECT COUNT(*)
     FROM UNNEST(JSON_EXTRACT_ARRAY(items, '$')) AS json_items
  ) AS quantity_variety_items,
  
  -------------  value ---------------  
  STRUCT 
  (
    SAFE_CAST(total AS FLOAT64) AS total,
    ( 
      SELECT sum (SAFE_CAST(JSON_VALUE(json_items,"$.price") AS FLOAT64)) 
        FROM UNNEST(JSON_EXTRACT_ARRAY(items, '$')) AS json_items
    ) AS items,    
    SAFE_CAST(discount AS FLOAT64) AS discounts,
    SAFE_CAST(JSON_VALUE (shipping, "$.price") AS FLOAT64) AS shipping,
    SAFE_CAST(tax AS FLOAT64) AS tax
  ) 
  AS value,

  -------------  payment -------------  
  STRUCT 
  (
    ${dooca.payment_method_group("JSON_VALUE (payment ,\"$.method\")")} AS method,
    JSON_VALUE (payment ,"$.method") || " - " || JSON_VALUE (payment ,"$.name") AS method_detail -- TEMP
  )
  AS payment,

  -------------  address -------------  
  STRUCT 
  (
    JSON_VALUE(address, '$.zipcode') AS shipping_zipcode,
    JSON_VALUE(address, '$.city') AS shipping_city,
    JSON_VALUE(address, '$.state') AS shipping_state,
    "Brasil" AS shipping_country
  ) 
  AS address,

  -------------  customer ------------  
  STRUCT 
  (
    SAFE_CAST(customer_id AS STRING) AS customer_id,
    JSON_VALUE(customer, '$.name') AS name,
    JSON_VALUE(customer, '$.cgc') AS doc_number,
    JSON_VALUE(customer, '$.email') AS email,
    JSON_VALUE(customer, '$.phone') AS phone
  ) 
  AS customer,

  -------------  items  ------------  

 ARRAY(
    SELECT AS STRUCT
      ID.*
    FROM ${ref("stand_dooca_order_items")} ID
    WHERE ID.order_id = O.id
  ) AS items

FROM
  ${ref("raw_dooca_Orders")} O
