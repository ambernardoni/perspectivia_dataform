config {
    description: "Padronização da tabela de produtos por pedido",
    schema: "standard",
    type: "view",
    columns: {
        order_id: "Identificador único do pedido (string)",
        product_id: "Identificador único do produto (string)",
        quantity: "Quantidade do produto no pedido (int64)",
        price_unit: "Preço unitário do produto (float64)",
        price_discount: "Valor do desconto aplicado ao produto (float64)",
        price_total: "Preço total do produto no pedido (float64)"
    },
    tags: ['magento']
}

WITH OrderItems AS (
  SELECT
    entity_id AS order_id,
    JSON_EXTRACT_ARRAY(items) AS items_array
  FROM
     ${ref("raw_magento_Orders")}
)

SELECT
  SAFE_CAST (order_id AS STRING) AS order_id,
  JSON_EXTRACT_SCALAR(item, '$.product_id') AS product_id,
  JSON_EXTRACT_SCALAR(item, '$.sku') as product_sku,

  SAFE_CAST(JSON_EXTRACT_SCALAR(item, '$.qty_ordered') AS INT64) AS quantity,
  SAFE_CAST(JSON_EXTRACT_SCALAR(item, '$.price') AS FLOAT64) AS price_unit,
  0 as price_discount,
  SAFE_CAST(JSON_EXTRACT_SCALAR(item, '$.qty_ordered') AS INT64) * CAST(JSON_EXTRACT_SCALAR(item, '$.price') AS FLOAT64) AS price_total
FROM
  OrderItems,
  UNNEST(items_array) AS item

