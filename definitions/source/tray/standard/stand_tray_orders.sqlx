config {
    description: "Padronização da tabela de pedidos",
    type: "view",
    schema: "standard",
    columns: {
        _airbyte_extracted_at: "Data e hora da extração dos dados pelo Airbyte",
        order_id: "Identificador único do pedido",
        creation_date: "Data e hora de criação do pedido",
        update_date: "Data e hora da última atualização do pedido",
        status_type: "Tipo de status do pedido",
        status_type_detail: "Detalhes do status do pedido",
        point_of_sale: "Ponto de venda onde o pedido foi realizado",
        value: "Estrutura contendo informações de valor do pedido",
        "value.total": "Valor total do pedido",
        "value.items": "Valor dos itens (temporariamente definido como 0)",
        "value.discounts": "Valor total dos descontos aplicados",
        "value.shipping": "Valor do frete",
        "value.tax": "Valor dos impostos (atualmente usando o valor do desconto)",
        payment: "Estrutura contendo informações de pagamento",
        "payment.status_type": "Tipo de status do pagamento",
        "payment.status_detail": "Detalhes do status do pagamento",
        "payment.method": "Método de pagamento utilizado",
        "payment.method_detail": "Detalhes do método de pagamento",
        address: "Estrutura contendo informações de endereço",
        "address.shipping_zipcode": "CEP de entrega",
        "address.shipping_city": "Cidade de entrega",
        "address.shipping_state": "Estado de entrega",
        "address.shipping_country": "País de entrega",
        customer: "Estrutura contendo informações do cliente",
        "customer.customer_id": "Identificador único do cliente",
        "customer.name": "Nome do cliente",
        "customer.doc_number": "Número do documento do cliente",
        "customer.email": "Email do cliente",
        "customer.phone": "Telefone do cliente"
    },
    tags: ['tray']
}

  --------------------------------------------------------------------------------------------------
  -- Converte as informações no formato da plataforma para o formato da PERSPECTIVIA
  -- A maior parte da lógica de transformação de formatos está aqui.
  -- DONWSTREAM: Essa view será usada pelo processamento geral (independente de plataforma]
  --------------------------------------------------------------------------------------------------
SELECT
  _airbyte_extracted_at,
  SAFE_CAST(id AS STRING) AS order_id,
  SAFE_CAST(date AS DATETIME) AS creation_date,
  SAFE_CAST(modified AS DATETIME) AS update_date,

  -- Status
   ${tray.order_status_type("status")} AS status_type,
  status AS status_type_detail,
  point_sale AS point_of_sale,

  -------------  value ---------------
  STRUCT ( 
      SAFE_CAST(total AS FLOAT64) AS total,
      0 AS items, --#############
      SAFE_CAST(discount AS FLOAT64) AS discounts,
      SAFE_CAST(shipment_value AS FLOAT64) AS shipping,
      SAFE_CAST(discount AS FLOAT64) AS tax 
  ) AS value,

  -------------  payment -------------
  STRUCT
  (
   ${tray.order_status_type("status")} AS status_type,
   status AS status_detail,
   payment_form AS method,
   payment_form AS method_detail
  )
  AS payment,

  -------------  address -------------
  STRUCT
  (
    "" AS shipping_zipcode,   --#####
    "" AS shipping_city,      --#####
    "" AS shipping_state,     --#####
    "" AS shipping_country    --#####
  )
  AS address,

  -------------  customer ------------
  STRUCT
  (
     SAFE_CAST(customer_id AS STRING) AS  customer_id,
     "" AS name,       --#####
     "" AS doc_number, --#####
     "" AS email,      --#####
     "" AS phone       --#####
  )
  AS customer,
FROM
  ${ref("raw_tray_Orders")} O
