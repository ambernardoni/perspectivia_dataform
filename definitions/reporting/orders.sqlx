config {
    type: "table",
    schema: "reporting",
    description: "Pedidos realizados na loja",
    tags: ['1-general'],

    bigquery: {
      partitionBy: "DATE(creation_date)"
    },
    
    assertions: {
      uniqueKey: ["order_id"],
      nonNull: ["order_id", "creation_date", "value.total", "status_type"],
      rowConditions: [
        'status_type IN ("Confirmado", "Cancelado", "Pendente", "Outros")',
        'payment.status_type IN ("Confirmado", "Cancelado", "Pendente", "Outros")'
      ]
    }
}

-- Tira os pedidos duplicados. Em uma futura versão em que faça a carga incrementa
-- deveríamos usar os recursos do próprio Dataform na declaração da carga incremental
-- 
WITH
  order_deduplicated AS (
  SELECT
    *,
    ROW_NUMBER() OVER (PARTITION BY order_id ORDER BY _airbyte_extracted_at DESC) AS rn
  FROM
    ${ref("stand_"+dataform.projectConfig.vars.ecommPlatName+"_orders")}
)



----------------------- MONTA A TABELA DE PEDIDOS ----------------------
SELECT
  *,

-------------------------- Itens do pedido 
-- Faz um join com prod_cat_order, conta as quantidades e cria um array com os pedidos
  (
    SELECT COUNT(*)
    FROM ${ref("stand_"+dataform.projectConfig.vars.ecommPlatName+"_prod_cat_order")} prod_cat_order
    WHERE prod_cat_order.order_id = O.order_id
  ) AS quantity_variety_items,

  (
    SELECT SUM(prod_cat_order.quantity)
    FROM ${ref("stand_"+dataform.projectConfig.vars.ecommPlatName+"_prod_cat_order")} prod_cat_order
    WHERE prod_cat_order.order_id = O.order_id
  ) AS quantity_items,

 ARRAY(
    SELECT AS STRUCT
      prod_cat_order.*
    FROM ${ref("stand_"+dataform.projectConfig.vars.ecommPlatName+"_prod_cat_order")} prod_cat_order
    WHERE prod_cat_order.order_id = O.order_id
 ) AS items,


---------------------  customer_stats   
  STRUCT ( 
    order_id,
    MIN(creation_date) OVER (PARTITION BY customer.customer_id) AS first_order_date,
    LAG(creation_date) OVER (PARTITION BY customer.customer_id ORDER BY creation_date) AS previous_order_date,
    DATE_DIFF(creation_date, LAG(creation_date) OVER (PARTITION BY customer.customer_id ORDER BY creation_date), DAY) AS days_since_last_order,
    ROW_NUMBER() OVER (PARTITION BY customer.customer_id ORDER BY creation_date) - 1 AS number_of_previous_orders
  ) AS customer_stats,


FROM
  order_deduplicated O

WHERE
  rn = 1 -- tira os registros duplicados

